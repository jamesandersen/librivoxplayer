<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" width="100%"
	useHandCursor="true" buttonMode="true"
	verticalScrollPolicy="off" horizontalScrollPolicy="off">
	<mx:states>
		<mx:State name="full">
			<mx:AddChild creationPolicy="">
				<mx:Text id="txtDescription" width="100%"/>
			</mx:AddChild>
		</mx:State>
	</mx:states>
	<mx:transitions>
        <mx:Transition fromState="*" toState="*">
            <mx:Resize target="{this}" />
        </mx:Transition>
    </mx:transitions>
	<mx:Script>
		<![CDATA[
			import org.jandersen.lvp.model.LibriVoxItem;
			import mx.controls.List;
			import org.jandersen.lvp.events.RequestFeedEvent;
			import mx.controls.Alert;
			import mx.events.FlexEvent;
			import com.adobe.cairngorm.control.CairngormEventDispatcher;
			
			public static const STATE_FULL:String = "full";
			
			protected override function createChildren():void{
				super.createChildren();
				addEventListener(MouseEvent.CLICK, handleMouseClick);
			}
			
			protected override function measure():void{
				if(!isNaN(this.explicitWidth)){
					this.measuredWidth = this.explicitWidth;
					txtTitle.explicitWidth = (1 - (btnPlay.percentWidth/100)) * this.explicitWidth;
					if(txtDescription) txtDescription.explicitWidth = this.explicitWidth;
				}
				this.measuredHeight = txtTitle.height 
					+ getStyle("paddingTop") + getStyle("paddingBottom") + 15;
				if(txtDescription != null && txtDescription.parent != null){
					measuredHeight += txtDescription.height;
				} 
				
			}
			
			public override function set data(value:Object):void{
				super.data = value;
				if(data is LibriVoxItem){
					var lvi:LibriVoxItem = data as LibriVoxItem;
					currentState = lvi.selected ? STATE_FULL : "";
					txtTitle.text = lvi.title;
					if(txtDescription) txtDescription.htmlText = lvi.description;
				}
			}
			
			protected function handleMouseClick(e:MouseEvent):void{
				if(currentState != STATE_FULL){
					currentState = STATE_FULL;
					if(data is LibriVoxItem){
						(data as LibriVoxItem).selected = true;
					}
				}else{
					if(data is LibriVoxItem){
						(data as LibriVoxItem).selected = false;
					}
					currentState = "";
				}
			}
			
			protected function btnPlay_click(e:MouseEvent):void{
				var feedEvent:RequestFeedEvent = new RequestFeedEvent();
				feedEvent.data = data;
				feedEvent.dispatch();
			}
			
		]]>
	</mx:Script>
	<mx:HBox paddingLeft="0" paddingRight="0" paddingBottom="0" paddingTop="0" horizontalGap="0"
		horizontalScrollPolicy="off">
		<mx:Text id="txtTitle" width="85%" styleName="itemTitle"/>
		<mx:Button id="btnPlay" label="Play" fontSize="8" width="8%" height="20" right="3" 
			click="btnPlay_click(event)"/>
	</mx:HBox>
	
	
</mx:VBox>
